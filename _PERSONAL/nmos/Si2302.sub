*Feb. 28, 2005
*Doc. ID: 78253, S-50232, Rev. B
*File Name: Si2302ADS_PS.txt and Si2302ADS_PS.lib
.SUBCKT Si2302 4 1 2
M1  3 1 2 2 NMOS W=259996u L=0.25u
M2  2 1 2 4 PMOS W=259996u L=0.80u
R1  4 3     RTEMP 9E-3
CGS 1 2     200E-12
DBD 2 4     DBD
**************************************************************
.MODEL  NMOS       NMOS  (LEVEL  = 3           TOX    = 3E-8
+ RS     = 22E-3          RD     = 0           NSUB   = 1.95E17
+ KP     = 2.3E-5         UO     = 650
+ VMAX   = 0              XJ     = 5E-7        KAPPA  = 5E-2
+ ETA    = 1E-4           TPG    = 1
+ IS     = 0              LD     = 0
+ CGSO   = 0              CGDO   = 0           CGBO   = 0
+ NFS    = 0.8E12         DELTA  = 0.1)
***************************************************************
.MODEL  PMOS       PMOS  (LEVEL  = 3           TOX    = 3E-8
+NSUB    = 1.6E17         TPG    = -1)
***************************************************************
.MODEL DBD D (CJO=90E-12 VJ=0.38 M=0.20
+RS=0.1 FC=0.5 IS=1E-12 TT=5E-8 N=1 BV=20.3)
***************************************************************
.MODEL RTEMP RES (TC1=10E-3 TC2=5.5E-6)
***************************************************************
.ENDS
